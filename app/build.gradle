apply plugin: 'com.android.application'

android {
  compileSdkVersion 33
  namespace "com.aptoide.iabexample"

  flavorDimensions = ["iab", "network"]

  defaultConfig {
    applicationId "com.appcoins.trivialdrivesample"
    minSdkVersion 21
    targetSdkVersion 33
    versionCode 29
    versionName "1.5.8"
    multiDexEnabled true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  buildFeatures {
    buildConfig true
  }

  signingConfigs {
    release {
      storeFile = file(project.BDS_WALLET_STORE_FILE)
      storePassword = project.BDS_WALLET_STORE_PASSWORD
      keyAlias = project.BDS_WALLET_KEY_ALIAS
      keyPassword = project.BDS_WALLET_KEY_PASSWORD
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationVariants.all { variant -> renameArtifact(defaultConfig)
      }
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      applicationVariants.all { variant -> renameArtifact(defaultConfig)
      }
    }
  }

  productFlavors {
    googleiab {
      dimension "iab"
      buildConfigField "String", "IAB_BIND_ACTION",
          "\"" + project.GOOGLE_IAB_BIND_ACTION + "\""
      buildConfigField "String", "IAB_KEY",
          "\"" + project.GOOGLE_IAB_KEY + "\""
      buildConfigField "String", "IAB_BIND_PACKAGE",
          "\"" + project.GOOGLE_IAB_BIND_PACKAGE + "\""
      buildConfigField "String", "IAB_UPDATE_ACTION",
          "\"" + project.GOOGLE_IAB_UPDATE_ACTION + "\""
      manifestPlaceholders = [iabPermission: project.GOOGLE_IAB_PERMISSION]
    }

    appcoinssdk {
      dimension "iab"
      buildConfigField "String", "IAB_UPDATE_ACTION",
          "\"" + project.APPCOINS_IAB_UPDATE_ACTION + "\""
      manifestPlaceholders = [iabPermission: project.APPCOINS_IAB_PERMISSION]
      applicationVariants.all { variant ->
        if (variant.buildType.getName().equalsIgnoreCase("debug")) {
          buildConfigField "String", "IAB_BIND_ACTION",
              "\"" + project.APPCOINS_DEV_IAB_BIND_ACTION + "\""
          buildConfigField "String", "IAB_BIND_PACKAGE",
              "\"" + project.APPCOINS_DEV_IAB_BIND_PACKAGE + "\""
          buildConfigField "String", "IAB_WALLET_ADDR",
              "\"" + project.APPCOINS_DEV_IAB_WALLET_ADDR + "\""
          buildConfigField "String", "IAB_KEY",
              "\"" + project.APPCOINS_DEV_IAB_KEY + "\""
        } else {
          buildConfigField "String", "IAB_KEY",
              "\"" + project.APPCOINS_IAB_KEY + "\""
          buildConfigField "String", "IAB_BIND_ACTION",
              "\"" + project.APPCOINS_IAB_BIND_ACTION + "\""
          buildConfigField "String", "IAB_BIND_PACKAGE",
              "\"" + project.APPCOINS_IAB_BIND_PACKAGE + "\""
          buildConfigField "String", "IAB_WALLET_ADDR",
              "\"" + project.APPCOINS_IAB_WALLET_ADDR + "\""
        }
      }
    }

    appcoinsiab {
      dimension "iab"
      buildConfigField "String", "IAB_UPDATE_ACTION",
          "\"" + project.APPCOINS_IAB_UPDATE_ACTION + "\""
      manifestPlaceholders = [iabPermission: project.APPCOINS_IAB_PERMISSION]
      applicationVariants.all { variant ->
        if (variant.buildType.getName().equalsIgnoreCase("debug")) {
          buildConfigField "String", "IAB_BIND_ACTION",
              "\"" + project.APPCOINS_DEV_IAB_BIND_ACTION + "\""
          buildConfigField "String", "IAB_BIND_PACKAGE",
              "\"" + project.APPCOINS_DEV_IAB_BIND_PACKAGE + "\""
          buildConfigField "String", "IAB_WALLET_ADDR",
              "\"" + project.APPCOINS_DEV_IAB_WALLET_ADDR + "\""
          buildConfigField "String", "IAB_KEY",
              "\"" + project.APPCOINS_DEV_IAB_KEY + "\""
        } else {
          buildConfigField "String", "IAB_KEY",
              "\"" + project.APPCOINS_IAB_KEY + "\""
          buildConfigField "String", "IAB_BIND_ACTION",
              "\"" + project.APPCOINS_IAB_BIND_ACTION + "\""
          buildConfigField "String", "IAB_BIND_PACKAGE",
              "\"" + project.APPCOINS_IAB_BIND_PACKAGE + "\""
          buildConfigField "String", "IAB_WALLET_ADDR",
              "\"" + project.APPCOINS_IAB_WALLET_ADDR + "\""
        }
      }
    }

    mainNetwork {
      dimension "network"
      buildConfigField "boolean", "TEST_NETWORK", "false"
      buildConfigField "String", "BACKEND_HOST",
          "\"" + project.BACK_END_HOST_PROD + "\""
      buildConfigField "String", "WALLET_PACKAGE",
          "\"" + project.APPCOINS_IAB_BIND_PACKAGE + "\""
      buildConfigField "String", "GAMESHUB_PACKAGE",
          "\"" + project.GAMESHUB_PACKAGE + "\""
    }

    ropstenNetwork {
      dimension "network"
      applicationIdSuffix ".test"
      buildConfigField "boolean", "TEST_NETWORK", "true"
      buildConfigField "String", "BACKEND_HOST",
          "\"" + project.BACK_END_HOST_DEV + "\""
      buildConfigField "String", "WALLET_PACKAGE",
          "\"" + project.APPCOINS_DEV_IAB_BIND_PACKAGE + "\""
      buildConfigField "String", "GAMESHUB_PACKAGE",
          "\"" + project.GAMESHUB_PACKAGE_DEV + "\""
    }
  }
}

dependencies {
  api(files("libs/aptoide-analytics.aar"))
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.google.code.gson:gson:2.10.1'
  debugImplementation 'io.reactivex.rxjava2:rxjava:2.2.19'
  debugImplementation 'com.madgag.spongycastle:prov:1.58.0.0'
  debugImplementation 'com.indicative.client.android:Indicative-Android:1.1.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"

  debugImplementation(files("libs/android-appcoins-billing-debug.aar"))
  debugImplementation(files("libs/appcoins-billing-debug.aar"))
  debugImplementation(files("libs/appcoins-adyen-debug.aar"))
  debugImplementation(files("libs/communication-debug.aar"))
  debugImplementation(files("libs/appcoins-ads-debug.aar"))
  debugImplementation(files("libs/appcoins-contract-proxy-debug.aar"))
  debugImplementation(files("libs/appcoins-core-debug.aar"))
  debugImplementation(files("libs/appcoins-lifecycle-debug.aar"))

  releaseImplementation "io.catappult:android-appcoins-billing:$sdk"
  releaseImplementation "io.catappult:appcoins-ads:$sdk_legacy"
  releaseImplementation "io.catappult:appcoins-contract-proxy:$sdk_legacy"
}

def renameArtifact(defaultConfig) {
  android.applicationVariants.all { variant ->
    variant.outputs.all {
      def fileName = "trivial-drive-appcoins-sample-${defaultConfig.versionName}_${variant.name}"
      outputFileName = new File("${fileName}.apk")
    }
  }
}
